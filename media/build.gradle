plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'com.google.devtools.ksp'
}

def propertiesFile = rootProject.file("media/src/main/assets/version.properties")
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

android {
    namespace 'com.truvideo.sdk.media'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode properties.getProperty("versionCode").toInteger()
        versionName properties.getProperty("versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }

    flavorDimensions = ['environments']

    productFlavors {
        beta {
            dimension 'environments'
        }
        rc {
            dimension 'environments'
        }
        prod {
            dimension 'environments'
        }
    }
}

dependencies {
    implementation "androidx.startup:startup-runtime:1.1.1"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'

    // S3
    implementation 'com.amazonaws:aws-android-sdk-mobile-client:2.16.6'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.44.0'
    implementation 'com.amazonaws:aws-android-sdk-cognito:2.16.6'

    // Room
    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"
    ksp "androidx.room:room-compiler:2.6.1"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "org.mockito:mockito-core:5.4.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
    testImplementation "io.mockk:mockk:1.13.5"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Common
    var commonVersion = properties.getProperty("commonVersion")
    implementation("com.github.truvideo:truvideo-sdk-android-common:$commonVersion")
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId 'com.truvideo'
                setArtifactId 'sdk-media'
                version android.defaultConfig.versionName

                def environment = properties.getProperty("environment")
                if (environment == "beta") {
                    artifact bundleBetaReleaseAar
                } else if (environment == "rc") {
                    artifact bundleRcReleaseAar
                } else {
                    artifact bundleProdReleaseAar
                }

                pom.withXml {
                    //noinspection GroovyImplicitNullArgumentCall
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}