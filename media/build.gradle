plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def propertiesFile = rootProject.file("media/src/main/assets/version.properties")
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

android {
    namespace 'com.truvideo.sdk.media'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode properties.getProperty("versionCode").toInteger()
        versionName properties.getProperty("versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'

    implementation 'com.amazonaws:aws-android-sdk-mobile-client:2.16.6'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.44.0'
    implementation 'com.amazonaws:aws-android-sdk-cognito:2.16.6'
    implementation 'com.amazonaws:aws-android-sdk-auth-userpools:2.16.6'
    implementation 'com.amazonaws:aws-android-sdk-auth-ui:2.6.10'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "org.mockito:mockito-core:5.4.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
    testImplementation "io.mockk:mockk:1.13.5"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //Common
    var commonVersion = properties.getProperty("commonVersion")
    implementation("com.github.Truvideo:truvideo-sdk-android-common:$commonVersion")

    var ktorVersion = "2.3.2"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2")
    implementation("io.ktor:ktor-client-core:$ktorVersion")
    implementation("io.ktor:ktor-client-logging:$ktorVersion")
    implementation("io.ktor:ktor-client-okhttp:$ktorVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1")
    implementation("com.russhwolf:multiplatform-settings:1.0.0")
    implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.4.0")
    implementation("co.touchlab:kermit:2.0.0-RC5")
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId 'com.truvideo.sdk'
                setArtifactId 'media'
                version android.defaultConfig.versionName
                artifact bundleReleaseAar

                pom.withXml {
                    //noinspection GroovyImplicitNullArgumentCall
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}